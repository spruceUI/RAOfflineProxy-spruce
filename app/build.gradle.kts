import java.util.Properties
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.ksp)
}

val keystoreProperties = Properties().also { props ->
    val propsFile = rootProject.file("keystore.properties")
    if (propsFile.exists()) props.load(propsFile.inputStream())
}

val e2eRaHost = providers.gradleProperty("e2eRaHost").getOrElse("http://10.0.2.2:8181")

android {
    namespace = "com.raofflineproxy"
    compileSdk = 37

    defaultConfig {
        applicationId = "com.raofflineproxy"
        minSdk = 26
        targetSdk = 36
        versionCode = 31
        versionName = "2.0.0-alpha1"
        buildConfigField("String", "RA_HOST", "\"https://retroachievements.org\"")
        buildConfigField("String", "RA_MEDIA_HOST", "\"https://media.retroachievements.org\"")
        buildConfigField("String", "USAGE_STATS_URL", "\"https://ud63psmdb5.execute-api.eu-central-1.amazonaws.com/usage/ping\"")
    }

    signingConfigs {
        create("release") {
            storeFile = keystoreProperties["storeFile"]?.let { rootProject.file(it) }
            storePassword = keystoreProperties["storePassword"] as String?
            keyAlias = keystoreProperties["keyAlias"] as String?
            keyPassword = keystoreProperties["keyPassword"] as String?
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig = signingConfigs.getByName("release")
        }
        create("e2e") {
            initWith(getByName("debug"))
            matchingFallbacks += listOf("debug")
            buildConfigField("String", "RA_HOST", "\"$e2eRaHost\"")
            buildConfigField("String", "RA_MEDIA_HOST", "\"$e2eRaHost\"")
            buildConfigField("String", "USAGE_STATS_URL", "\"$e2eRaHost/usage/ping\"")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        aidl = true
        viewBinding = true
        buildConfig = true
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
        }
    }

    ndkVersion = "28.0.13004108"

    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

androidComponents {
    onVariants(selector().withBuildType("release")) { variant ->
        variant.outputs.forEach { output ->
            val versionName = output.versionName.orNull ?: return@forEach
            output.outputFileName.set("RAOfflineProxy-v${versionName}.apk")
        }
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.lifecycle.viewmodel.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.fragment.ktx)
    implementation(libs.room.runtime)
    implementation(libs.room.ktx)
    ksp(libs.room.compiler)
    implementation(libs.okhttp)
    implementation(libs.coroutines.android)
    implementation(libs.swiperefreshlayout)
    implementation(libs.coil)
    implementation(libs.documentfile)
    implementation(libs.recyclerview)
    implementation(libs.shizuku.api)
    implementation(libs.shizuku.provider)
    implementation(variantOf(libs.zstd.jni) { artifactType("aar") })
    testImplementation(libs.junit)
    testImplementation(libs.zstd.jni)
    testImplementation(libs.org.json)
}
